#!/bin/zsh
emulate -L zsh; setopt $_edit_opts

local wordchars
zstyle -s ":edit:$WIDGET:" word-chars wordchars &&
    local +h WORDCHARS="$wordchars"

[[ $WIDGET == *kill-* ]] &&
  zle -f kill

local -i end start=$CURSOR
local subword="([[:WORD:]]##~*[[:lower:]]*[[:upper:]]*~*[[:alnum:]]*[^[:alnum:]]*)"
local nonword='([^[:WORD:]]##)'
if [[ $WIDGET == *backward-* ]]; then
  (( end = start - ${(M)#LBUFFER%%($~subword|$~nonword~*[[:space:]]*[[:punct:]]*)(?|)} ))
else
  (( end = start + ${(M)#RBUFFER##(?|)($~subword|$~nonword~*[[:punct:]]*[[:space:]]*)} ))
fi

(( end == start )) &&
  return 1

case $WIDGET in
  *kill-*)
    (( REGION_ACTIVE )) ||
      CURSOR=$start
    MARK=$end
    zle .kill-region
    ;;
  *backward-*)
    zle .backward-char -n $(( start - end ))
    ;;
  *)
    zle .forward-char -n $(( end - start ))
    ;;
esac

return 0
