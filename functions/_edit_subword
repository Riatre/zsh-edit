#!/bin/zsh
setopt localoptions $_edit_opts
local +h -i MARK

() {
  emulate -L zsh; setopt $_edit_opts

  local subword="([[:WORD:]]##~*[[:lower:]]*[[:upper:]]*~*[[:alnum:]]*[^[:alnum:]]*)"
  local nonword='([^[:WORD:]]##)'

  if [[ $WIDGET == *backward-* ]]; then
    (( MARK = CURSOR - ${(M)#LBUFFER%%($~subword|$~nonword~*[[:space:]]*[[:punct:]]*)(?|)} ))
  else
    (( MARK = CURSOR + ${(M)#RBUFFER##(?|)($~subword|$~nonword~*[[:punct:]]*[[:space:]]*)} ))
  fi
}

(( MARK != CURSOR ))
local -i ret=$?

case $WIDGET in
  *kill-*)
    zle .kill-region
    zle -f kill
    ;;
  *backward-*)
    zle .backward-char -n $(( CURSOR - MARK ))
    ;;
  *)
    zle .forward-char -n $(( MARK - CURSOR ))
    ;;
esac

return ret
