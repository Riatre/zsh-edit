#!/bin/zsh
emulate -L zsh; setopt $_edit_opts

if [[ $WIDGET == *kill-* ]]; then
  zle -f kill
  (( REGION_ACTIVE )) ||
      MARK=$CURSOR
fi

if [[ $WIDGET == *-shell-word ]]; then
  local w=
  if [[ $WIDGET == *backward* ]]; then
    local -a words=( ${(Z+cn+)LBUFFER} )
    while [[ -z $w && $#words[@] -gt 0 ]]; do
      w=${(M)LBUFFER%$words[-1]*}
      shift -p words
    done
    (( CURSOR -= $#w ))
  else
    # We can't split $RBUFFER on words, because that usually doesn't parse correctly.
    local -a words=( ${(Z+cn+)BUFFER} ) lwords=( ${(Z+cn+)LBUFFER} )
    words=( $words[$#lwords[@],-1] )
    words[1]=${words[1]#$lwords[-1]}
    while [[ -z $w && $#words[@] -gt 0 ]]; do
      w=${(M)RBUFFER#*$words[1]}
      shift words
    done
    (( CURSOR += $#w ))
  fi
else
  local wordchars
  zstyle -s ":edit:$WIDGET:" word-chars wordchars &&
      local +h WORDCHARS="$wordchars"

  local subword='([[:WORD:]]##~*[^[:upper:]]*[[:upper:]]*~*[[:alnum:]]*[^[:alnum:]]*)'
  local word="(${subword}|[^[:WORD:][:space:]]##|[[:space:]]##)"
  if [[ $WIDGET == *backward-* ]]; then
    (( CURSOR -= ${(M)#LBUFFER%%${~word}(?|)} ))
  else
    (( CURSOR += ${(M)#RBUFFER##(?|)${~word}} ))
  fi
fi

[[ $WIDGET == *kill-* ]] &&
    zle .kill-region

return 0
