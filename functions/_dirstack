#!/bin/zsh
zmodload zsh/complist
autoload -Uz _dirstack_

_dirstack() {
  setopt localoptions $_edit_opts

  if [[ $LASTWIDGET != $WIDGET ]]; then
    zle .push-line
    if [[ -o autocd ]]; then
      LBUFFER='~'
    else
      LBUFFER='cd '
    fi
    case $KEYS in
      *[-_] )
        LBUFFER+='-'
        ;;
      *[=+] )
        LBUFFER+='+'
        ;;
      * )
        if [[ -o pushdminus ]]; then
          LBUFFER+='-'
        else
          LBUFFER+='+'
        fi
        ;;
    esac
  fi
  case $LBUFFER in
    *'-' )
      bindkey -M menuselect '^[-' menu-complete
      bindkey -M menuselect '^[=' reverse-menu-complete
      ;;
    *'+' )
      bindkey -M menuselect '^[=' menu-complete
      bindkey -M menuselect '^[-' reverse-menu-complete
      ;;
  esac
  zle _dirstack_

  case $_lastcomp[nmatches] in
    0 )
      BUFFER=''
      zle .get-line
      return 1
      ;;
    1 )
      zle .accept-line
      return 0
      ;;
    * )
      return 0
      ;;
  esac
}

zle -C _dirstack_ menu-select _dirstack_
_dirstack "$@"
