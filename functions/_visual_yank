#!/bin/zsh
autoload -Uz _show_clipboard
zle -C ._show_clipboard list-choices _show_clipboard

_visual_yank() {
  zle -f yank

  local -i len=$#killring[@]
  {
    () {
      emulate -L zsh; setopt $_edit_opts

      local -aU kills=( $CUTBUFFER $killring[@] )
      shift kills
      killring=( ${(@)kills[@]:#} )

      if [[ $WIDGET == reverse-* ]]; then
        local widget=.${WIDGET#reverse-}
        local -i i
        for (( i = 0; i < $#killring[@]; i++ )); do
          zle $widget
        done
      else
        zle .$WIDGET
      fi

      ZLS_COLORS="=${(zj: :)${(b)BUFFER[YANK_START+1,YANK_END]}//=/\=}=07"
    }

    zle ._show_clipboard

  } always {
    () {
      emulate -L zsh; setopt $_edit_opts

      (( len > $#killring[@] )) &&
          killring+=( "${(s:.:@)${(r:$((len - $#killring[@] - 1))::.:):-}}" )
    }
  }
}

_visual_yank "$@"
